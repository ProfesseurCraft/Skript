# =============================================================
#                            OPTIONS
# =============================================================

options:
    # Open source color scheme (https://yeun.github.io/open-color/ingredients.html)
    darkgray: <##495057>
    gray: <##adb5bd>
    lightgray: <##e9ecef>
    red: <##ff6b6b>
    pink: <##f06595>
    grape: <##cc5de8>
    violet: <##845ef7>
    lightviolet: <##d0bfff>
    indigo: <##5c7cfa>
    blue: <##339af0>
    cyan: <##22b8cf>
    teal: <##20c997>
    green: <##51cf66>
    lime: <##94d82d>
    yellow: <##fcc419>
    orange: <##ff922b>

# =============================================================
#                           ÉVÉNEMENTS
# =============================================================

# Colorisation chat
# -----------------
on chat:
    cancel event
    broadcast "[{@darkgray}Chat&r] [{@grape}%player%&r] : {@lightgray}%message%"

# Sauvegarde du lieu de décès
# ---------------------------
on death:
    if victim is a player:
        set {lastDeathLocation.%victim's uuid%} to victim's location
        send "-------------------------" to victim
        send "{@blue}/back {@gray}pour revenir au lieu de votre mort" to victim
        send "-------------------------" to victim

on explode:
    cancel event
# =============================================================
#                           COMMANDES
# =============================================================

# Tous les joueurs
# ----------------

command /spawn:
    trigger:
        teleport player to world's spawn point

command /back:
    trigger:
        if {lastDeathLocation.%player's uuid%} is set:
            teleport player to {lastDeathLocation.%player's uuid%}
            send "-------------------------"
            send "{@green}Vous avez été téléporté à votre dernier lieu de décès."
            send "-------------------------"
        else:
            send "-------------------------"
            send "{@gray}Vous n'avez aucun lieu de décès enregistré."
            send "-------------------------"

command /sharelink <text> <text>:
    description: Partager un lien en le rendant cliquable
    usage: /sharelink <lien> <texte du lien>
    trigger:
        send "-------------------------"
        send formatted "{@lightviolet}Lien partagé : &r<link:%arg-1%><tooltip:{@lightviolet}Cliquez pour ouvrir dans un navigateur>%arg-2%<reset>" to player
        send "-------------------------"

# Admins uniquement
# -----------------

command /setspawn:
    executable by: console
    trigger:
        set world's spawn point to player's location
        send "-------------------------"
        send "{@green}Vous avez bien redéfini le point de spawn du monde"
        send "-------------------------"

command /killentities <int>:
    executable by: console
    trigger:
        loop all entities in radius int-arg around player:
            if loop-entity is not the player:
                kill loop-entity